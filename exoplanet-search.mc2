'60VEPqKkQ5KmAbCWR1taQ6Ks5kT72qKsQr0gNLt_S3tpQL8_RV3l1JgkBINpD1skBF_2Iot5HK8CGJF3Aa4kR0CdQmtrNM8kBI.iNaxjS6CoRa4k15CWQM0WOqt_05hkQ671PLF8B2.oCIRkDIFt3LxXRqKmSa4oPLxiIbKhNaKm.I35RrGWS6Kn1JCEIZO8JZp4G0N.4V.Q...LV8YxyuXRvVFLVCYfbHVLwik18V.NJlyA1..........fNL28E8uzMcM0sNpMg5WcP9W7ksrMRnJRrbVi9JMXiQtcN50VVE6BT39GtAkwg_PhLVrYTURLB2.kkb_mLZ.t.0Tgpjl_....4VsG..' UNWRAP 'exoplanet.model'  STORE

$token AUTHENTICATE
10000000 MAXOPS

// coeff used to only have the deepest dropouts
2.2 'coeff' STORE

// max dtw score accepted
1.3 'max.score.dtw' STORE

// minimum number of anomaly per star needed
3 'min.number.anomaly' STORE

[ $token '~.*' {  }  ] FIND 
//[ SWAP [] { 'status' 'CONFIRMED' } filter.byattr ] FILTER

// get a subset of the 150k stars
[ 87000 DUP 40 +  ] SUBLIST


// creating regexp for ids
<% LABELS 'id' GET %> FOREACH '|' DEPTH 1 - JOIN '~(' SWAP ')' '' 3 JOIN 'ids' STORE

// cool id to debug
//'004570949' 'ids' STORE

// fetch the datapoints
[ $token 'kepler.sap.flux' { 'id' $ids } NOW NOW ] FETCH

[] 'star.result' STORE
<%
    'star' STORE
    
    // if the star has a confirmed planet, adding it as a label
    $star DUP ATTRIBUTES  RELABEL 'star' STORE 
    
    // downsampling and splitting the observations
    [ $star  bucketizer.mean 0 90 m 0 ] BUCKETIZE 0 GET 
    2 h 10 'observationNumber' TIMESPLIT 'observations' STORE 
    $observations
    [] 'exoplanet.result' STORE
    
    <%
        
        'observation' STORE
        
        // calculating standard deviation on a sliding window
        [ $observation true mapper.sd 5 5 0 ] MAP 0 GET
        
        DUP TRUE MUSIGMA + 'value' STORE
        [ SWAP $value mapper.gt 0 0 0 ] MAP NONEMPTY
        DUP <% SIZE 0 == %> <% DROP CONTINUE %> IFT
        
        // generating mask according
        [ SWAP T mapper.replace 0 0 0 ] MAP 'annotation' RENAME 0 GET 'mask' STORE 
        [ [ $mask ]  [ $observation ] [] op.mask ] APPLY 0 GET 
        2 h 2 'exoplanetNumber' TIMESPLIT 
        <%
            'exoplanet' STORE
            
            // checking that outlier is the min
            [ $exoplanet bucketizer.min 0 0 1 ] BUCKETIZE 0 GET VALUES 
            <% DUP SIZE 0 == %> <% DROP CONTINUE %> IFT
            0 GET 'min' STORE
            $exoplanet 1 T ESDTEST
            <% DUP SIZE 0 == %> <% DROP CONTINUE %> IFT
            0 GET 'outlier' STORE
            $exoplanet TICKLIST 0 GET <% $outlier == %> <% CONTINUE %> IFT
            $exoplanet TICKLIST DUP SIZE 1 - GET <% $outlier == %> <% CONTINUE %> IFT
            NEWGTS 'outlier' RENAME  
            $outlier NaN NaN NaN $exoplanet $outlier ATTICK 4 GET ADDVALUE 
            [ SWAP bucketizer.min 0 0 1 ] BUCKETIZE 0 GET  VALUES 0 GET 'outlier.min' STORE
            $exoplanet TRUE MUSIGMA $coeff * - <% $outlier.min <= %> <% CONTINUE %> IFT
            <% $outlier.min $min != %> <% CONTINUE %> IFT
            
            // checking DTW
            $exoplanet NORMALIZE $exoplanet.model NORMALIZE 0.0 DTW  
            DUP <% $max.score.dtw >= %> <% DROP CONTINUE %> IFT
            $exoplanet [ SWAP T mapper.replace 0 0 0 ] MAP
            SWAP { SWAP 'score' SWAP TOSTRING } RELABEL
        
            // Creating GTS with transit time
            NEWGTS 'kepler.transit.diff' RENAME $exoplanet LABELS RELABEL $outlier NaN NaN NaN $exoplanet TRUE MUSIGMA + $outlier.min - ADDVALUE
            2 ->LIST FLATTEN
            
        %> FOREACH 
        
        DEPTH ->LIST FLATTEN
        DUP <% SIZE 0 == %> <% DROP CONTINUE %> IFT
        DUP 
        [ SWAP [] 'kepler.sap.flux' filter.byclass ] FILTER
        [ SWAP [] reducer.or.exclude-nulls ] REDUCE 0 GET 'kepler.exoplanet.detection' RENAME
        SWAP 
        [ SWAP [] 'kepler.transit.diff' filter.byclass ] FILTER
        [ SWAP [] reducer.sum ] REDUCE 0 GET
        
         $observation 3 ->LIST [ SWAP ] $exoplanet.result APPEND 'exoplanet.result' STORE
    %> FOREACH

    $exoplanet.result
    DUP <% SIZE 0 == %> <% DROP CONTINUE %> IFT
    
    DUP FLATTEN [ SWAP [] 'kepler.transit.diff' filter.byclass ] FILTER
    [ SWAP [] reducer.sum ] REDUCE 0 GET
    <% DUP VALUES SIZE $min.number.anomaly <= %> <% DROP DROP CONTINUE %> IFT
    SWAP 
    <%
        DUP [ SWAP [] 'kepler.sap.flux' filter.byclass ] FILTER 0 GET 
        SWAP [ SWAP [] 'kepler.exoplanet.detection' filter.byclass ] FILTER 0 GET
        2 ->LIST
    %> FOREACH
    DEPTH ->LIST
    [ SWAP ] $star.result APPEND  'star.result' STORE
%> FOREACH
$star.result
