'60VEPqKkQ5KmAbCWR1taQ6Ks5kX701tWR6.IPLwiSq4mR23kAa8jQrGnS68WR.WYNLpkNLSiOFGfR5lm1JgkBIFmCXskBV_2Iot5HK8CGJF8Hn.lC27qAY.n1JC0IZG8G34JGF8dO.ZkBI3lBY7sDIFGQq8nOM8qNMGdQqtDSLpXOM71CIJ5RrGWS6Kn1JCEIZO8JZp4G._AB23oBYNiB238FoxDGZ_HIJK34V.L.0g3X._AB23oBYNiB278FoxDGZ_HIJK31JgkBIFmCXskBk_2FJt3HJG0K3J5RrGWS6Kn1JCEIZO8JZp4G._AB23oBYNiB238FoxDGZ_HIJK3.0P.whbmVdni00P.YOuI2GPer.Va.0X4.WyA1..........fNL1t9GHaRSU0sRPfokoB2.kX2KBjIObCsb2YxRMvzzzz4qbcbITOpy65ask47BbzJu1H2Fz.PX5HflBHZ6WjjN.w8WYjlRw58AZrMRYuMVNm7EBLmHCOyDcHBixCM2kHvwLj0JYuEVVf7E4Tx1EAjJw0Pjg6SySGOATwnJIajOO3SgiQ5e.dY6.tljj7dVFYyy6SnkC79dyZ77U3IISZreK_m2uu_ia.ABT.QDPKIVNZBmwr7fjguWKZ_FzAZ6rvohJ0HSwQRyGsTBB1JI_fnn7dcCySJTAj_YmHzxtecUVj0IaJMZp5SgifYHWlukXm6F0cqV.dJF7..0OJ3F.' UNWRAP 'exoplanet.model' STORE

1765000000 'bucketspan' STORE

// find all
//[ $token '~.*' { 'campagne' 'kplr' }  ] FIND [ SWAP [] { 'status' 'CONFIRMED' } filter.byattr ] FILTER [ 800 DUP 3 + ] SUBLIST
// or test 
NEWGTS { 'id' '006541920' } RELABEL NEWGTS { 'id' '011804465' } RELABEL NEWGTS { 'id' '006850504' } RELABEL NEWGTS { 'id' '211089792' } RELABEL
DEPTH ->LIST

// looping through the star
<%
    LABELS 'id' GET 'id' STORE
    [ $token 'kepler.sap.flux' { 'id' $id } NOW NOW ] FETCH 0 GET 'star' STORE
    
    MARK
    
    // downsampling
    [ $star bucketizer.mean 0 $bucketspan 0 ] BUCKETIZE 
    [ SWAP mapper.min 2 2 0 ] MAP 0 GET 
    
    // split the star into multiple observations if there's 2 h without datapoints
    2 h 10 'observationNumber' TIMESPLIT
    // looping through the observationsâ€¨
    <%
        'observation' STORE
        MARK
        $observation 32 4 8 10 true 0.0 ZDISCORDS
        1 d 10 'discordNumber' TIMESPLIT
         <%
            'discord' STORE
            
            // checking that min is the outlier
            [ $discord bucketizer.min 0 0 1 ] BUCKETIZE 0 GET VALUES 
             <% DUP SIZE 0 == %> <% CONTINUE %> IFT
             0 GET 'min' STORE
            $discord 1 T ESDTEST 
            <% DUP SIZE 0 == %> <%  DROP CONTINUE %> IFT
            0 GET 'outlier' STORE
            $discord $outlier ATTICK 4 GET  <% $min != %> <% CONTINUE %> IFT
            
            // checking that outlier is not the first or last tick
            $discord FIRSTTICK <% $outlier == %> <% CONTINUE %> IFT
            $discord LASTTICK <% $outlier == %> <% CONTINUE %> IFT
            
            // Checking DTW
            $discord $exoplanet.model 0.0 DTW
            <% 3.0 > %> <% CONTINUE %> IFT
            $discord 'kepler.sap.flux.detection' RENAME
         %> FOREACH
         $observation
         COUNTTOMARK ->LIST SWAP DROP
        <% DUP SIZE 1 == %> <%  DROP CONTINUE %> IFT
    %> FOREACH
    COUNTTOMARK ->LIST SWAP DROP
    <% DUP SIZE 0 == %> <%  DROP CONTINUE %> IFT
%> FOREACH
